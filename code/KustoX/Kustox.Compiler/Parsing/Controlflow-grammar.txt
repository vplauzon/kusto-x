#	Comments & interleaves
rule(interleave=false) comment = "//" (. - ("\r" | "\n"))*;
interleave = (" " | "\r" | "\n" | "\t") | comment;

#	tokens
rule(interleave=false) identifier = ("a".."z" | "A".."Z") ("a".."z" | "A".."Z" | "0".."9")* => text;
rule(interleave=false) number = ("0".."9")+;
rule(interleave=false) double = "-"? number* ("." number+)?;
rule(interleave=false) character = normal:(. - ("\"" | "\r" | "\n" | "\\"))
	| escapeQuote:("\\" "\"") | escapeBackslash:"\\\\"
	| escapeLetter:("\\" l:("n" | "r" | "t" | "v"))
	| escapeHexa:("\\x" h:("0".."9" | "a".."f" | "A".."F"){1,2});
rule(interleave=false) singleQuotableString = ((. - "'") | "\\'")* => text;
rule(interleave=false) doubleQuotableString = ((. - '"') | '\\"')* => text;
rule(interleave=false) singleQuotedString = "'" quotedString:singleQuotableString "'" => quotedString;
rule(interleave=false) doubleQuotedString = '"' quotedString:doubleQuotableString '"' => quotedString;
rule(interleave=false) quotedString = doubleQuotedString | singleQuotedString;
rule(interleave=false) integer = number+ => integer(text);
rule(interleave=false) boolean = "true" | "false" => boolean(text);
rule(interleave=false) blockSpacer = " " | "\t" | "\r";

#	rules

#	Property rules
rule propertyValue = boolean:boolean | integer:integer | string:quotedString;
rule propertyAssignation = id:identifier "=" value:propertyValue => merge({"id" : id}, value);
rule propertyAssignationList = head:propertyAssignation
	tail:("," pa:propertyAssignation => pa)* => prepend(head, tail);
rule withPropertyAssignationList = "with" "(" list:propertyAssignationList? ")" => flatten(list);
rule optionalWithPropertyAssignationList = withPropertyAssignationList? => flatten(defaultOutput);

# queries
rule(interleave=false) kqlLine = ((. - "\n")* - blockSpacer*) "\n"=> text;
rule(interleave=false) query = (. - ".") kqlLine* => text;
rule(interleave=false) command = "." kqlLine+ => text;

#	Control flows
rule captureScalar = "@capture-scalar" id:identifier "="
	runnable:(query:query | command:command) => {
		"captureName":id,
		"isScalarCapture":true,
		"runnable":runnable,
		"code":text
	};
rule captureTable = "@capture" id:identifier "=" runnable:(query:query | command:command) => {
		"captureName":id,
		"isScalarCapture":false,
		"runnable":runnable,
		"code":text
	};
rule execCommand = command => {
		"runnable":{
			"command":defaultOutput
		},
		"code":text
	};
rule sequenceItem = captureScalar | captureTable | execCommand => {
		"capturable":defaultOutput
	};
rule sequenceItemList = head:sequenceItem
	tail:sequenceItem* => prepend(head, tail);
rule sequenceContent = "{" sequenceItems:sequenceItemList? "}"
	=> flatten(sequenceItems);
rule controlFlow = "@control-flow"
	properties:optionalWithPropertyAssignationList
	rootSequence:sequenceContent => {
		"rootSequence":{
			"blocks":rootSequence,
			"code":text
		}
	};

#	main rule
rule main = controlFlow;